//  iceberg
//
//  inputs:
//      client
//          rq_nw
//          rq_md
//          rq_cc
//      server
//          cf_nw
//          cf_cc
//          cf_ex
//      internal
//          timer


[init]
    rq_nw               ->  w_cf_nw             /   send_rq_nw


[w_cf_nw]
    rq_nw               ->  error               /   send_rj_nw      send_rq_cc
    rq_md               ->  modifying_w_nw
    rq_cc               ->  canceling_w_nw

    cf_nw               ->  market
    cf_ex               ->  error               /   send_cf_ex      send_rq_cc
    cf_cc               ->  error

    timer
        &   timeout     ->  error               /   send_rq_cc
                        ->  w_cf_nw


[modifying_w_nw]
    rq_nw               ->  error               /   send_rj_nw      send_rq_cc
    rq_md               ->  modifying_w_nw
    rq_cc               ->  canceling_w_nw

    cf_nw               ->  modifying_w_cc      /   send_rq_cc
    cf_ex               ->  error               /   send_cf_ex      send_rq_cc
    cf_cc               ->  error

    timer
        &   timeout     ->  error               /   send_rq_cc
                        ->  modifying_w_nw


[modifying_w_cc]
    rq_nw               ->  error               /   send_rj_nw      send_rq_cc
    rq_md               ->  modifying_w_cc
    rq_cc               ->  canceling_w_cc

    cf_nw               ->  error               /   send_rq_cc
    cf_ex
        &   full_cli    ->  end                 /   send_cf_ex
        &   full_mk     ->  end                 /   send_cf_ex      send_cf_cc
        &   partial_mk  ->  modifying_w_cc      /   send_cf_ex
    cf_cc               ->  modifying_w_nw      /   send_rq_nw

    timer
        &   timeout     ->  modifying_w_cc      /   warning_timer
                        ->  modifying_w_cc


[canceling_w_nw]
    rq_nw               ->  error               /   send_rj_nw      send_rq_cc
    rq_md               ->  canceling_w_nw      /   send_rj_md
    rq_cc               ->  canceling_w_nw      /   send_rq_cc

    cf_nw               ->  canceling_w_cc      /   send_rq_cc
    cf_ex               ->  error               /   send_cf_ex
    cf_cc               ->  error

    timer
        &   timeout     ->  canceling_w_nw      /   warning_timer
                        ->  canceling_w_nw


[market]
    rq_nw               ->  error               /   send_rj_nw      send_rq_cc
    rq_md               ->  modifying_w_cc      /   send_rq_cc
    rq_cc               ->  canceling_w_cc      /   send_rq_cc

    cf_nw               ->  error               /   send_rq_cc
    cf_ex
        &   full_cli    ->  end                 /   send_cf_ex
        &   full_mk     ->  w_cf_nw             /   send_cf_ex      send_rq_nw
        &   partial_mk  ->  market              /   send_cf_ex
    cf_cc               ->  error               /   send_cf_cc

    timer               ->  market


[canceling_w_cc]
    rq_nw               ->  error               /   send_rj_nw      send_rq_cc
    rq_md               ->  canceling_w_cc      /   send_rj_md
    rq_cc               ->  canceling_w_cc      /   send_rq_cc

    cf_nw               ->  error               /   send_rq_cc
    cf_ex
        &   full_cli    ->  end                 /   send_cf_ex
        &   full_mk     ->  end                 /   send_cf_ex      send_cf_cc
        &   partial_mk  ->  canceling_w_cc      /   send_cf_ex
    cf_cc               ->  end                 /   send_cf_cc

    timer
        &   timeout     ->  canceling_w_nw      /   warning_timer
                        ->  canceling_w_nw


[end]
    _                   ->  error

[error]
    _                   ->  error2      /   rq_cc


[error2]
    _                   ->  error2
